#Использовать 1commands

Функция СоздатьТаблицуСтатистики(СтрокаЗаголовков)

    ТаблицаСтатистики = Новый ТаблицаЗначений();

    ТаблицаСтатистики.Колонки.Добавить("start");
    ТаблицаСтатистики.Колонки.Добавить("end");
    
    Для Каждого Заголовок Из СтрРазделить(СтрокаЗаголовков, ",") Цикл
        ТаблицаСтатистики.Колонки.Добавить(Заголовок);    
    КонецЦикла;

    Возврат ТаблицаСтатистики;

КонецФункции

Процедура ЗаписатьТаблицуСтатистики(ТаблицаСтатистики)

    МассивСтрок = Новый Массив;
    МассивОписанияКолонок = Новый Массив;
    Для каждого Колонка Из ТаблицаСтатистики.Колонки Цикл
        МассивОписанияКолонок.Добавить(Колонка.Имя);    
    КонецЦикла;

    МассивСтрок.Добавить(СтрСоединить(МассивОписанияКолонок, ";"));

    Для Каждого Строка Из ТаблицаСтатистики Цикл

        ОписаниеСтроки = Новый Массив;
        Для каждого Колонка Из ТаблицаСтатистики.Колонки Цикл
            ОписаниеСтроки.Добавить(Строка[Колонка.Имя]);    
        КонецЦикла;
        МассивСтрок.Добавить(СтрСоединить(ОписаниеСтроки, ";"));

    КонецЦикла;

    ИтоговыйТекст = СтрСоединить(МассивСтрок, Символы.ПС);

    ЗаписьТекста = Новый ЗаписьТекста("git_stat.csv");
	ЗаписьТекста.Записать(ИтоговыйТекст);
	ЗаписьТекста.Закрыть();

КонецПроцедуры

СтрокаЗаголовков = "author,insertions,insertions_per,deletions,deletions_per,files,files_per,commits,commits_per,lines_changed,lines_changed_per";
ТаблицаСтатистики = СоздатьТаблицуСтатистики(СтрокаЗаголовков);

НачалоПериода = '2023-01-30';
Неделя = 60*60*24*7;
КонецПериода = НачалоПериода + Неделя - 1;

Пока КонецПериода <= ТекущаяДата() Цикл

    КомандныйФайл = Новый КомандныйФайл;
    КомандныйФайл.УстановитьПриложение("C:\Program Files\Git\bin\bash.exe");
    КомандныйФайл.УстановитьКодировкуВывода(КодировкаТекста.UTF8);
    КомандныйФайл.Создать("",".sh");

    КомандныйФайл.ДобавитьКоманду(СтрШаблон("export _GIT_SINCE=%1%2%1", """", Формат(НачалоПериода, "ДФ=yyyy-MM-dd")));
    КомандныйФайл.ДобавитьКоманду(СтрШаблон("export _GIT_UNTIL=%1%2%1", """", Формат(КонецПериода, "ДФ=yyyy-MM-dd")));
    КомандныйФайл.ДобавитьКоманду("cd C:/GIT/Retail2DEV");
    КомандныйФайл.ДобавитьКоманду("export _GIT_BRANCH=master");
    КомандныйФайл.ДобавитьКоманду("C:/GIT/git-quick-stats/git-quick-stats -V");

    КодВозврата = КомандныйФайл.Исполнить();
    Вывод = КомандныйФайл.ПолучитьВывод();

    Вывод = СтрЗаменить(Вывод, СтрокаЗаголовков, "");
    Вывод = СокрЛП(Вывод);
    
    Для Каждого СтрокаВывода Из СтрРазделить(Вывод, Символы.ВК) Цикл
        
        Если СтрНайти(СтрокаВывода, СтрокаЗаголовков) > 0 Тогда
            Продолжить;
        КонецЕсли;

        ЭлементыСтрокиВывода = СтрРазделить(СокрЛП(СтрокаВывода), ",", Ложь);

        НоваяСтрока = ТаблицаСтатистики.Добавить();
        НоваяСтрока.start = НачалоПериода;
        НоваяСтрока.end = КонецПериода;
        НоваяСтрока.author = ЭлементыСтрокиВывода[0];
        НоваяСтрока.insertions = ЭлементыСтрокиВывода[1];
        НоваяСтрока.insertions_per = ЭлементыСтрокиВывода[2];
        НоваяСтрока.deletions = ЭлементыСтрокиВывода[3];
        НоваяСтрока.deletions_per = ЭлементыСтрокиВывода[4];
        НоваяСтрока.files = ЭлементыСтрокиВывода[5];
        НоваяСтрока.files_per = ЭлементыСтрокиВывода[6];
        НоваяСтрока.commits = ЭлементыСтрокиВывода[7];
        НоваяСтрока.commits_per = ЭлементыСтрокиВывода[8];
        НоваяСтрока.lines_changed = ЭлементыСтрокиВывода[9];
        НоваяСтрока.lines_changed_per = ЭлементыСтрокиВывода[10];
                
    КонецЦикла;

    НачалоПериода = НачалоПериода + Неделя;
    КонецПериода = КонецПериода + Неделя;
    
КонецЦикла;

ЗаписатьТаблицуСтатистики(ТаблицаСтатистики);